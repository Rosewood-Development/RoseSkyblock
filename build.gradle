plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.0' apply false
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
    id 'maven-publish'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group 'dev.rosewood'
    version '1.0.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()

        maven { url = 'https://libraries.minecraft.net' }
        maven { url = 'https://papermc.io/repo/repository/maven-public/' }
        maven { url = 'https://maven.enginehub.org/repo/' }
        maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'https://repo.codemc.org/repository/maven-public' }
        maven { url = 'https://repo.rosewooddev.io/repository/public/' }

        maven {
            url = 'https://repo.rosewooddev.io/repository/maven-public/'
            credentials {
                username project.mavenUser
                password project.mavenPassword
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS-Wrapper')
    implementation project(':NMS-v1_13_R2')
    implementation project(':NMS-v1_14_R1')
    implementation project(':NMS-v1_15_R1')
    implementation project(':NMS-v1_16_R1')
    implementation project(':NMS-v1_16_R2')
    implementation project(':NMS-v1_16_R3')
}

shadowJar {
    archiveClassifier.set(null)

    relocate('kotlin', 'dev.rosewood.roseskyblock.lib.kotlin')
    relocate('org.jetbrains.annotations', 'dev.rosewood.roseskyblock.lib.annotations')
    relocate('org.intellij.lang.annotations', 'dev.rosewood.roseskyblock.lib.annotations')
    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseskyblock.lib.rosegarden')
    relocate('dev.rosewood.guiframework', 'dev.rosewood.roseskyblock.lib.guiframework')
    relocate('io.papermc.lib', 'dev.rosewood.roseskyblock.lib.paperlib')
    relocate('cloud.commandframework', 'dev.rosewood.roseskyblock.lib.commandframework')
    relocate('me.lucko.commodore', 'dev.rosewood.roseskyblock.lib.commodore')
    relocate('io.leangen.geantyref', 'dev.rosewood.roseskyblock.lib.geantyref')

    exclude('com/mojang/**')

    // TODO: Fix minimize. There is about 600kb of unused stuff in the output jar
    minimize {
        subprojects.each {
            exclude(project(it.path))
        }
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'roseskyblock'
            pom {
                name = 'roseskyblock'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar

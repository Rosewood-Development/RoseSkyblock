plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20' apply false
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    group 'dev.rosewood'
    version '1.0.0'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        jcenter()

        maven { url = 'https://libraries.minecraft.net' }
        maven { url = 'https://papermc.io/repo/repository/maven-public/' }
        maven { url = 'https://maven.enginehub.org/repo/' }
        maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'https://repo.codemc.org/repository/maven-public' }
        maven { url = 'https://repo.rosewooddev.io/repository/public/' }

        maven {
            url = 'https://repo.rosewooddev.io/repository/maven-public/'
            credentials {
                username project.mavenUser
                password project.mavenPassword
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}

configurations {
    childJars
}


dependencies {
    implementation project(':Plugin')
    implementation project(':NMS-Wrapper')
    implementation project(':NMS-v1_13_R2')
    implementation project(':NMS-v1_14_R1')
    implementation project(':NMS-v1_15_R1')
    implementation project(':NMS-v1_16_R1')
    implementation project(':NMS-v1_16_R2')
    implementation project(':NMS-v1_16_R3')

    subprojects.each {
        childJars project(it.path)
    }
}

shadowJar {
    archiveClassifier.set(null)
    minimize()

    relocate('kotlin', 'dev.rosewood.roseskyblock.lib.kotlin')
    relocate('org.jetbrains.annotations', 'dev.rosewood.roseskyblock.lib.annotations')
    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseskyblock.lib.rosegarden')
    relocate('dev.rosewood.guiframework', 'dev.rosewood.roseskyblock.lib.guiframework')
    relocate('io.papermc.lib', 'dev.rosewood.roseskyblock.lib.paperlib')
    relocate('cloud.commandframework', 'dev.rosewood.roseskyblock.lib.commandframework')
    relocate('me.lucko.commodore', 'dev.rosewood.roseskyblock.lib.commodore')
    relocate('io.leangen.geantyref', 'dev.rosewood.roseskyblock.lib.geantyref')

    exclude('com/mojang/**')
}

jar {
    dependsOn configurations.childJars
    from {
        configurations.childJars.collect {
            zipTree(it)
        }
    }
}

build.dependsOn shadowJar

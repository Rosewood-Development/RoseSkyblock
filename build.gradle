import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.20'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'dev.rosewood'
version '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
	jcenter()

    maven { url = 'https://libraries.minecraft.net' }
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'https://maven.enginehub.org/repo/' }
    maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://repo.rosewooddev.io/repository/public/' }

    maven {
        url = 'https://repo.rosewooddev.io/repository/maven-public'
        credentials {
            username project.mavenUser
            password project.mavenPassword
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

dependencies {
    shadow 'org.spigotmc:spigot:1.16.4'
    shadow 'com.sk89q.worldedit:worldedit-bukkit:7.2.0-SNAPSHOT'
    shadow 'me.clip:placeholderapi:2.10.4'

    implementation 'dev.rosewood:rosegarden:1.0.5-SNAPSHOT'
    implementation 'dev.rosewood:guiframework:1.1.1'
    implementation 'io.papermc:paperlib:1.0.5'

    implementation 'cloud.commandframework:cloud-paper:1.2.0'
    implementation 'me.lucko:commodore:1.9'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

shadowJar {
    archiveClassifier.set(null)
    minimize()

    relocate('kotlin', 'dev.rosewood.roseskyblock.lib.kotlin')
    relocate('org.jetbrains.annotations', 'dev.rosewood.roseskyblock.lib.annotations')
    relocate('dev.rosewood.rosegarden', 'dev.rosewood.roseskyblock.lib.rosegarden')
    relocate('dev.rosewood.guiframework', 'dev.rosewood.roseskyblock.lib.guiframework')
    relocate('io.papermc.lib', 'dev.rosewood.roseskyblock.lib.paperlib')
    relocate('cloud.commandframework', 'dev.rosewood.roseskyblock.lib.commandframework')
    relocate('me.lucko.commodore', 'dev.rosewood.roseskyblock.lib.commodore')
    relocate('io.leangen.geantyref', 'dev.rosewood.roseskyblock.lib.geantyref')

    exclude('com/mojang/**')
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
    }
}

build.dependsOn shadowJar
